# FindGLFW3.cmake
#
# GLFW: Graphics Library Framework
#
# This will define the following variables
#
#   GLFW3_FOUND
#   GLFW3_INCLUDE_DIR
#   GLFW3_LIBRARIES
#   GLFW3_RUNTIME_DIR
#
# and the following imported targets
#
#   GLFW3::GLFW3
#
# The following variables can be set as arguments
#
#   GLFW3_ROOT_DIR
#

FIND_PACKAGE(PkgConfig QUIET)
PKG_CHECK_MODULES(_GLFW3_PC QUIET glfw3)

SET(GLFW3_INCLUDE_DIR ${_GLFW3_PC_INCLUDE_DIRS})
FIND_PATH(
    GLFW3_INCLUDE_DIR
    NAMES GLFW/glfw3.h
    PATHS ${GLFW3_ROOT_DIR}
    PATH_SUFFIXES include/
)

FIND_PATH(
    GLFW3_RUNTIME_DIR
    NAMES glfw3.dll
    PATHS ${GLFW3_ROOT_DIR} ${_GLFW3_PC_LIBDIR} ${_GLFW3_PC_LIBRARY_DIRS} 
    PATH_SUFFIXES bin lib
)

SET(GLFW3_LIBRARY ${_GLFW3_PC_LIBRARIES})
FIND_LIBRARY(
    GLFW3_LIBRARY
    NAMES glfw3dll glfw3 glfw
    PATHS ${GLFW3_ROOT_DIR}
    PATH_SUFFIXES lib
)

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(
    GLFW3
    REQUIRED_VARS 
        GLFW3_INCLUDE_DIR
        GLFW3_LIBRARY 
)

ADD_LIBRARY(GLFW3::GLFW3 INTERFACE IMPORTED)
SET_TARGET_PROPERTIES(
    GLFW3::GLFW3 PROPERTIES
    INTERFACE_LINK_LIBRARIES "${GLFW3_LIBRARY}"
    INTERFACE_INCLUDE_DIRECTORIES "${GLFW3_INCLUDE_DIR}"
)
